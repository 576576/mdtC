::打印并换行
function void println(str){
	print(str)
	print("\n")
}

::安全型ubind, 牺牲少许效率
::绑定一个单位, 类型u.type, 标记u.fx, 保存到u; 跳转到bind.end
function void ubind_s(u.type,u.fx,u){
	if(u==null){
		::init
		ubind(u.type)
		u=@unit
	}
	ubind(u)
	jump(init).when(@unit.sensor(@dead)!=0)
	u.cer=@unit.sensor(@controller)
	if(u.cer!=@unit){
		jump(init).when(u.cer!=@this)
		jump(init).when(@unit.sensor(@flag)!=u.fx)
	}
	uctrl(flag,u.fx)
	jump(bind.end)
}

::非安全型ubind, 入口偏移+2
::绑定一个单位, 类型u.type, 标记u.fx, 保存到u; 跳转到bind.end
function void ubind_us(u.type,u.fx,u){
	::init
	ubind(u.type)
	u=@unit
	ubind(u)

	jump(init).when(@unit.sensor(@dead)!=0)
	u.cer=@unit.sensor(@controller)
	if(u.cer!=@unit){
		jump(init).when(u.cer!=@this)
		jump(init).when(@unit.sensor(@flag)!=u.fx)
	}
	uctrl(flag,u.fx)
	jump(bind.end)
}

::绑定多个单位ubinds
::绑定u.max个单位(上限u.lim个), 类型u.type, 标记u.f0~u.f0+(n-1), 保存到u1~un; 跳转到bind.end
function void ubinds(u.type,u.f0,u.max,u.lim){
	u.fx=u.f0+u.now
	u.now=u.now%u.max
	jump2(+(u.now*12+2))
	repeat(u,u.lim){
		ubind_us(u.type,u.fx,u)
	}
	::bind.end
	u.now=(u.now+1)%u.max
}

::根据地图大小生成flag
function flag mapBasedFlag(map.size,flag.bit){
	map.bit=ceil(lg(map.size))
	x.bit=10.^(map.bit+flag.bit)
	flag=@thisx*x.bit+@thisy*u.bit
}

::寻找核心, 保存到c
function void getCore(c){
	c.d=c.sensor(@dead)
	if(c.d!=0){
		do{
			c.ulocate(core)
		}while(c.f==0)
	}
}

::挖矿item, 投放到s
function void mine(item,s){
	s.x=s.sensor(@x)
	s.y=s.sensor(@y)
	u.item=@unit.sensor(@firstItem)
	u.count=@unit.sensor(item)
	if(u.item==item){
		jump(mine2ore).when(u.count<u.fill)
		::drop2target
		uctrl(approach,s.x,s.y,6)
		uctrl(itemDrop,s,u.capa)
		end()
	}
	if(u.item==null){
		::mine2ore
		m.ulocate(ore).ore(item)
		if(m.f==0){
			end()
		}
		uctrl(boost,1)
		uctrl(approach,m.x,m.y,5)
		uctrl(mine,m.x,m.y)
		end()
	}
	uctrl(approach,c.x,c.y,6)
	uctrl(itemDrop,c,u.capa)
}